plugins {
    id 'java'
    id "io.freefair.lombok" version "8.13"
    id "run.halo.plugin.devtools" version "0.6.1"
}

group 'com.kunkunyu.link.submit'

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.22.0')
    implementation 'cn.hutool:hutool-all:5.8.25'
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

tasks.register('processUiResources', Copy) {
    from project(':ui').layout.buildDirectory.dir('dist')
    into layout.buildDirectory.dir('resources/main/console')
    dependsOn project(':ui').tasks.named('assemble')
    shouldRunAfter tasks.named('processResources')
}

tasks.named('classes') {
    dependsOn tasks.named('processUiResources')
    dependsOn project(':packages:link-submit-widget').tasks.named('build')
}

halo {
    version = '2.22.1'
    superAdminUsername = 'admin'
    superAdminPassword = 'admin'
    externalUrl = 'http://localhost:8081'
    docker {
        // windows 需要改为 npipe:////./pipe/docker_engine
        // linux、macos 需要改为 unix:///var/run/docker.sock
        url = 'npipe:////./pipe/docker_engine'
        apiVersion = '1.47'
    }
    port = 8081

    // 开启 debug 断点模式
    debug = true
    debugPort = 5005

    containerName = 'link-submit'
    // 控制台会挂起，等待点击 Attach debugger 按钮
    suspend = true
}

haloPlugin {
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        // 用于定义 API 分组规则
        groupingRules {
            // 此名称为 group name，定义后 groupedApiMappings 中的 /v3/api-docs/ 后的名称需要与之相同，要避免与 halo 中已经存在的 group 相同避免生成后出现与插件无关的 API
            linkSubmitApi {
                // 分组显示名称
                displayName = 'Extension API for Plugin linkSubmit'
                // 分组的 API 规则
                pathsToMatch = ["/apis/link.submit.kunkunyu.com/v1alpha1/**",
                                "/apis/console.api.link.submit.kunkunyu.com/v1alpha1/**"
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/linkSubmitApi': 'linkSubmitApi.json'
        ]
        generator {
            // 默认配置可缺省
            outputDir = file("${projectDir}/ui/src/api/generated")
        }
    }

}
